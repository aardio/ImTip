import fonts.imtip;
import fsys;
import fsys.lnk;
import process;
import process.cache;
import process.popen;
import win.dlg.message;
import key.hotkey;
import key;
import mouse;
import winex;
import winex.mouse;
import winex.key;
import winex.accObject;
import ui.chineseNumber;
import string.chineseNumber;
import win.ui;
/*DSG{{*/
var winform = win.form(text="设置";right=984;bottom=589;bgcolor=16777215)
winform.add(
btnHelp={cls="plus";text="aardio 语法";left=824;top=28;right=933;bottom=50;align="left";color=3947580;dr=1;dt=1;font=LOGFONT(h=-13);iconColor=2960685;iconStyle={align="left";font=LOGFONT(h=-16;name='imtip')};iconText='\uF0EB';notify=1;paddingLeft=20;z=4};
btnReset={cls="plus";text='\uF021   恢复到范例';left=619;top=529;right=765;bottom=565;bgcolor=14935259;db=1;dr=1;font=LOGFONT(h=-16;name='imtip');notify=1;tabstop=1;z=6};
btnSave={cls="plus";text='\uF0C7   保存';left=777;top=529;right=923;bottom=565;bgcolor=11580047;db=1;dr=1;font=LOGFONT(h=-16;name='imtip');notify=1;tabstop=1;z=5};
chkEnableHotkey={cls="plus";text="启用超级热键";left=33;top=23;right=473;bottom=54;align="left";dl=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-15;name='imtip')};iconText='\uF0C8';notify=1;textPadding={left=24};z=2};
editTable={cls="edit";left=33;top=61;right=951;bottom=498;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=3};
lbTip={cls="static";left=39;top=523;right=594;bottom=574;db=1;dl=1;dr=1;transparent=1;z=1}
)
/*}}*/

import style; 
winform.chkEnableHotkey.skin(style.checkBox);

import config
import app.hotkey;
if(!config.hotkey.editTable){
	config.hotkey.editTable = app.hotkey.defaultCode; 
} 

winform.bindConfig( config.hotkey,{
	edit = "text";
	radiobutton = "checked";
	checkbox = "checked";
	combobox = "selIndex";
	plus ={
		["^chk"] = "checked";
		["^edit"] = "text";
	};
} );

winform.btnSave.skin(style.primaryButton)

var hotkeyMgr;
winform.btnSave.oncommand = function(id,event){
    config.hotkey.save();
    winform.btnSave.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
    if(!winform.chkEnableHotkey.checked){
    	if(hotkeyMgr){
    		hotkeyMgr.close();
    	    hotkeyMgr= null;
    	}
    	
    	win.delay(500);
    	winform.btnSave.disabledText = null;
    	return;
    }
    
    if(hotkeyMgr){
        hotkeyMgr.close();
    }
       
	hotkeyMgr =  key.hotkey(winform)
	var code = "{" + winform.editTable.text + "}"
	var keyTable
	try{
		keyTable = eval(code)
	}
	catch(e){
		var msgDlg = win.dlg.message(winform)
		msgDlg.err('超级热键配置代码错误:\n'+e) 
	}
	
	var err;
	try{
		if(type(keyTable)==type.table){
			for k,v in table.eachName(keyTable){ 
				hotkeyMgr.regStr(k,v);
			} 
		}		
	}
	catch(e){
		winform.editTable.showErrorTip(e);
		err = e;
	}
	if(err){
		winform.btnSave.disabledText = null;
		return;
	}
	
	win.delay(500);
	winform.btnSave.disabledText = null;
}

import fsys.dlg;
winform.editTable.enablePopMenu({
	{ /*---分隔线---*/ };
	{
		"保存到代码文件";
		function(id){
			var path = fsys.dlg.save("*.aardio|*.aardio||","保存到代码文件",,winform)
			if(path){
				string.save(path,winform.editTable.text )
				winform.editTable.modified = false;
			}
		} 
	};
	{
		"加载代码文件";
		function(id){
			var path = fsys.dlg.open("*.aardio|*.aardio","加载代码文件",,winform)
			if(path) winform.editTable.text = string.load(path)
		} 
	};
	{ /*---分隔线---*/ };
	{
		"恢复到范例";
		function(id){
			winform.editTable.text = app.hotkey.defaultCode;
			winform.editTable.modified = false;
		} 
	};
})

winform.btnReset.skin(style.button)
winform.btnReset.oncommand = function(id,event){
	winform.editTable.text = app.hotkey.defaultCode;
	winform.editTable.modified = false;
}
winform.editTable.modified = false;

winform.btnSave.oncommand();
if(_ARGV.tray){  
	winform.setTimeout( 
		function(){
			winform.btnSave.oncommand();
		},3000
	); 	
}

import process;
winform.btnHelp.oncommand = function(id,event){
	process.openUrl("http://www.aardio.com");
}

winform.btnHelp.skin(style.plainButton);

..mainHotkeyForm = winform;

winform.onDestroy = function(){
    ..mainHotkeyForm = null;
}
