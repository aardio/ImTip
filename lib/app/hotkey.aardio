import config;

namespace app.hotkey;

mainPath = ..io.appData("/aardio/std/ImTip/.hotkey/hotkey.aardio")
if(!..io.exist(mainPath)){
	..string.save(mainPath, $"\hotkey\hotkey.aardio")
}
 
var hotkeyThreadHandle;
var hotkeyThreadId;
update = function(enabled){
	if(hotkeyThreadId){ 
		::User32.PostThreadMessage(hotkeyThreadId,0x12/*_WM_QUIT*/,0,0);
		if( !..thread.waitOne(hotkeyThreadHandle,1000) ){
			..thread.terminate(hotkeyThreadHandle,0); 
			..io.print("异常终止热键线程");
		}
		else {
			..io.print("正常终止热键线程");
		} 
		..raw.closehandle(hotkeyThreadHandle)
		
		hotkeyThreadHandle = null;
		hotkeyThreadId = null;
	}
	
	if(!enabled) return; 	
		 
	hotkeyThreadHandle = ..thread.create( 
		function(){
			import process.aardio;
			process.aardio.run(
				function(){ 
					import win;
					import win.ui;
					
					//禁止在关闭最后一个独立窗口时自动退出消息循环。
					win.autoQuitMessage = false;
					
					mainPath = ..io.appData("/aardio/std/ImTip/.hotkey/hotkey.aardio");
					loadcodex(mainPath,"ImTip"); 
				} 
			)
		}
	)
	
	if(hotkeyThreadHandle){
		hotkeyThreadId = ::Kernel32.GetThreadId(hotkeyThreadHandle); 	
	}
	
}

var thrdWatcher;
edit = function(){
	import process.aardio;
	var path = process.aardio.getPath();
	if(!path){
		if(!process.aardio.download()) return;
	}
	
	if(!thrdWatcher){
		import fsys.dirWatcher;
		import win.debounce;
		
		var updateOnCodeChange = win.debounce(update,500);
		
		thrdWatcher = ..fsys.dirWatcher.thread(
			function(filename,action,actionText){ 
				updateOnCodeChange(..config.hotkey.chkEnableHotkey);
			}, ..io.appData("/aardio/std/ImTip/.hotkey/") ); 	
	}

	process.aardio(mainPath);
}

..subscribe("beforeUnload",function(){
	if(hotkeyThreadId){ 
		::User32.PostThreadMessage(hotkeyThreadId,0x12/*_WM_QUIT*/,0,0); 
		..raw.closehandle(hotkeyThreadHandle);
		
		hotkeyThreadHandle = null;
		hotkeyThreadId = null; 
	}
	
	if(thrdWatcher){
		thrdWatcher.close(); //停止监视文件
	}	
} );
