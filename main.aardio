
/*启动参数{{*/
_IMTIP_APP = true;
if(_ARGV.chat){ 
	loadcodex("\dlg\aiChat.aardio");
	return; 
}

for(k,v in _ARGV){
	if(string.endWith(v,".aardio",true)){
		if(io.exist(v)){
			__argvImTipConfig = v;
		}
	}
}
/*}}*/

import style;
import fonts.imtip;
import win.ui;
import win.ui.atom;
import win.dlg.message;
import fonts.fontAwesomeSub;
/*DSG{{*/
mainForm = win.form(text="ImTip 8.0 - 智能桌面助手";right=680;bottom=511;bgcolor=0xF5F5F5;border="none";max=false;min=false)
mainForm.add(
btnAiChat={cls="plus";text="打开助手";left=100;top=165;right=310;bottom=195;bgcolor=0x6B6BFF;border={radius=6};color=0xFFFFFF;dl=1;dt=1;font=LOGFONT(h=-13);notify=1;z=20};
btnClose={cls="plus";text="×";left=653;top=0;right=681;bottom=28;color=0xFFFFFF;dr=1;dt=1;font=LOGFONT(h=-22);notify=1;z=30};
btnFilterWindows={cls="plus";text="编辑规则";left=570;top=305;right=640;bottom=335;bgcolor=0x00A5FF;border={radius=6};color=0xFFFFFF;db=1;disabled=1;dr=1;font=LOGFONT(h=-13);notify=1;z=25};
btnHotkey={cls="plus";text="编辑热键";left=100;top=438;right=200;bottom=468;bgcolor=0xD77800;border={radius=6};color=0xFFFFFF;db=1;dl=1;font=LOGFONT(h=-13);notify=1;z=11};
btnImTip={cls="plus";text="配置";left=570;top=165;right=640;bottom=195;bgcolor=0x6BA800;border={radius=6};color=0xFFFFFF;dr=1;dt=1;font=LOGFONT(h=-13);notify=1;z=16};
cardAiChat={cls="plus";left=20;top=80;right=330;bottom=200;bgcolor=0xFFFFFF;border={color=0xFFE8E8E8;radius=12;width=1};dl=1;dt=1;z=3};
cardFilter={cls="plus";left=350;top=220;right=660;bottom=340;bgcolor=0xFFFFFF;border={color=0xFFE8E8E8;radius=12;width=1};db=1;dr=1;z=2};
cardHotkey={cls="plus";left=20;top=360;right=660;bottom=480;bgcolor=0xFFFFFF;border={color=0xFFE8E8E8;radius=12;width=1};db=1;dl=1;dr=1;z=5};
cardImeTip={cls="plus";left=350;top=80;right=660;bottom=200;bgcolor=0xFFFFFF;border={color=0xFFE8E8E8;radius=12;width=1};dr=1;dt=1;z=4};
cardSystem={cls="plus";left=20;top=220;right=330;bottom=340;bgcolor=0xFFFFFF;border={color=0xFFE8E8E8;radius=12;width=1};db=1;dl=1;z=1};
chkFilterWindows={cls="plus";text="扩展规则";left=430;top=299;right=530;bottom=319;align="left";bgcolor=0xFFFFFF;db=1;dr=1;iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=20};z=24};
chkHotkey={cls="plus";text="启用热键";left=220;top=443;right=320;bottom=463;align="left";bgcolor=0xFFFFFF;db=1;dl=1;iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=20};z=10};
chkImeBarDisabled={cls="plus";text="启用";left=430;top=170;right=510;bottom=190;align="left";bgcolor=0xFFFFFF;dr=1;dt=1;iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=20};z=15};
chkJab={cls="plus";text="启用 Java 程序支持（JetBrains 等）";left=100;top=299;right=310;bottom=324;align="left";bgcolor=0xFFFFFF;db=1;dl=1;iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=20};z=28};
chkSysRun={cls="plus";text="开机启动";left=100;top=274;right=200;bottom=299;align="left";bgcolor=0xFFFFFF;db=1;dl=1;iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=20};z=27};
chkWatchFullScreen={cls="plus";text="忽略全屏";left=430;top=274;right=530;bottom=294;align="left";bgcolor=0xFFFFFF;db=1;dr=1;iconStyle={align="left";font=LOGFONT(h=-14;name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=20};z=23};
iconAiChat={cls="plus";text='\uF031';left=40;top=96;right=90;bottom=146;bgcolor=0xFFFFFF;dl=1;dt=1;font=LOGFONT(h=-22;name='imtip');iconColor=0x6B6BFF;z=17};
iconFilter={cls="plus";text='\uE82E';left=370;top=236;right=420;bottom=286;bgcolor=0xFFFFFF;db=1;dr=1;font=LOGFONT(h=-22;name='imtip');iconColor=0x00A5FF;z=21};
iconHotkey={cls="plus";text='\uE8CE';left=40;top=380;right=90;bottom=430;bgcolor=0xFFFFFF;db=1;dl=1;font=LOGFONT(h=-22;name='imtip');iconColor=0xD77800;z=7};
iconImeTip={cls="plus";text='\uF0EB';left=370;top=96;right=420;bottom=146;bgcolor=0xFFFFFF;dr=1;dt=1;font=LOGFONT(h=-22;name='imtip');iconColor=0x6BA800;z=12};
iconSystem={cls="plus";text='\uF013';left=40;top=236;right=90;bottom=286;bgcolor=0xFFFFFF;db=1;dl=1;font=LOGFONT(h=-22;name='FontAwesome');iconColor=0x666666;z=31};
lblAiChatDesc={cls="plus";text="支持多种大模型
智能对话与代码生成";left=100;top=127;right=310;bottom=167;align="left";bgcolor=0xFFFFFF;color=0x666666;dl=1;dt=1;valign="top";z=19};
lblAiChatTitle={cls="plus";text="AI 智能助手";left=100;top=99;right=310;bottom=124;align="left";bgcolor=0xFFFFFF;color=0x333333;dl=1;dt=1;font=LOGFONT(h=-16;weight=600);z=18};
lblFilterTitle={cls="plus";text="提示规则";left=430;top=239;right=640;bottom=264;align="left";bgcolor=0xFFFFFF;color=0x333333;db=1;dr=1;font=LOGFONT(h=-16;weight=600);z=22};
lblHotkeyDesc={cls="syslink";text='强大的可编程热键，集成 <a href="https://www.aardio.com">aardio</a> 开发环境，快速接入大模型 API';left=100;top=410;right=640;bottom=435;bgcolor=0xFFFFFF;color=0x666666;db=1;dl=1;dr=1;valign="top";z=9};
lblHotkeyTitle={cls="plus";text="超级热键";left=100;top=385;right=310;bottom=410;align="left";bgcolor=0xFFFFFF;color=0x333333;db=1;dl=1;font=LOGFONT(h=-16;weight=600);z=8};
lblImeTipDesc={cls="static";text="智能显示输入法状态
支持多种窗口类型";left=430;top=127;right=640;bottom=167;bgcolor=0xFFFFFF;color=0x666666;dr=1;dt=1;valign="top";z=14};
lblImeTipTitle={cls="plus";text="输入提示";left=430;top=99;right=640;bottom=124;align="left";bgcolor=0xFFFFFF;color=0x333333;dr=1;dt=1;font=LOGFONT(h=-16;weight=600);z=13};
lblSystemTitle={cls="plus";text="系统选项";left=100;top=239;right=210;bottom=264;align="left";bgcolor=0xFFFFFF;color=0x333333;db=1;dl=1;font=LOGFONT(h=-16;weight=600);z=26};
lpHotkey={cls="syslink";text='<a href="https://imtip.aardio.com">软件官网</a> | <a href="https://www.aardio.com/zh-cn/doc/?q=library-guide%2Fstd%2Fkey%2Fhotkey.html">开发指南</a> | <a href="https://www.aardio.com/zh-cn/doc/?q=guide%2Flanguage%2Fsyntax-quick-ref.html">语法速览</a>';left=377;top=444;right=640;bottom=465;align="right";bgcolor=0xFFFFFF;db=1;dr=1;notify=1;transparent=1;z=29};
titleBar={cls="bk";text="    ImTip 智能桌面助手 8.0";left=0;top=0;right=682;bottom=60;align="left";bgcolor=0xD77800;color=0xFFFFFF;dl=1;dr=1;dt=1;font=LOGFONT(h=-18;weight=600);textPadding={left=20;top=15};z=6}
)
/*}}*/

var atom,hwnd = mainForm.atom("E474890D-1DFA-4575-B456-7B46C15665DC.imtip");
if(!atom){
	if(__argvImTipConfig) win.sendCopyData(hwnd,__argvImTipConfig );
	else {
		win.sendCopyData(hwnd,"show");
	}
	
	win.quitMessage();	
	return;
}

import fsys.update.simpleMain;
if( fsys.update.simpleMain(
	"ImTip 桌面助手",
	"http://imtip.aardio.com/update/",
	io.appData("/aardio/std/ImTip/app/update"),
	function(version,description,status){})){
	return 0;	
}

mainForm.chkSysRun.skin(style.checkBox); 
mainForm.chkHotkey.skin(style.checkBox);
mainForm.chkJab.skin(style.checkBox);
mainForm.chkFilterWindows.skin(style.checkBox);
mainForm.chkWatchFullScreen.skin(style.checkBox);
mainForm.chkImeBarDisabled.skin(style.checkBox);
mainForm.btnHotkey.skin(style.button);
mainForm.btnAiChat.skin(style.button);
mainForm.btnImTip.skin(style.button);

mainForm.cardAiChat.directDrawBackgroundOnly();
mainForm.cardImeTip.directDrawBackgroundOnly();
mainForm.cardFilter.directDrawBackgroundOnly();
mainForm.cardHotkey.directDrawBackgroundOnly();
mainForm.cardSystem.directDrawBackgroundOnly();

mainForm.btnAiChat.skin({
    color =={default= style.button.color};
    background = {
        default = 0xFFFF6B6B;
        hover = 0xFFFF5252;
        active = 0xFFC64920;
        disabled = 0xFFCCCCCC;
    };
});
mainForm.btnImTip.skin({
    color =={default= style.button.color};
    background = {
        default = 0xFF00A86B;
        hover = 0xFF008F5A;
        active = 0xFF3E6842;
        disabled = 0xFFCCCCCC;
    };
});
mainForm.btnFilterWindows.skin({
    color =={default= style.button.color};
    background = {
        default = 0xFFFFA500;
        hover = 0xFFFF8C00;
        active = 0xFFFF7300;
        disabled = 0xFFCCCCCC;
    };
});

mainForm.enableDpiScaling("init");

import win.ui.shadow;
win.ui.shadow(mainForm);

import config;
if(!_ARGV.sys){
	if(!_ARGV.updated){
		mainForm.show()
	}
	else {
		if(!config.setting.runOnce){
			mainForm.show();
			config.setting.runOnce = true;
			config.setting.save();
		}
	}  
}
 
//显示输入状态栏
import key.ime.stateBar;
imeBar = key.ime.stateBar(mainForm);

imeBar.timeout = config.setting.timeout;
imeBar.interval = config.setting.interval;
imeBar.editorClasses = config.setting.editorClasses;
imeBar.quirksMode = config.setting.updateQuirksMode();

//下面创建托盘图标
import win.util.tray;
mainForm.tray = win.util.tray(mainForm)  
mainForm.tray.tip = "ImTip：智能桌面助手" //设置鼠标提示 

//响应托盘消息
mainForm.onTrayMessage = {
	[0x205/*_WM_RBUTTONUP*/] = function(wParam){ 
		import ui.appTrayMenu;
		var menu = ui.appTrayMenu(mainForm); //延迟创建,避免系统启动时 DPI 缩放前创建菜单
		
	    win.setForeground(mainForm.hwnd); //避免不点击菜单不会消失，父窗口隐藏也要这样做
	    menu.popup(); 
	};
	[0x201/*_WM_LBUTTONDOWN*/] = function(wParam){ 
	 	if(..mainSettingForm) ..mainSettingForm.show(false);
	 	
	 	if(::User32.GetKeyState(0x11/*_VK_CTRL*/) & 0x8000){
	 		..config.setting.imeBarDisabled = !..imeBar.paused; 
			..imeBar.imeWatch(!..config.setting.imeBarDisabled);
			..mainForm.chkImeBarDisabled.checked = !..config.setting.imeBarDisabled;
			..config.setting.save();
			
			win.setForeground(mainForm.hwnd);
	 	}
	 	elseif(::User32.GetKeyState(0x10/*_VK_SHIFT*/) & 0x8000){
	 		mainForm.show(false); 
	 		
	 		import process.imTip; 
			process.imTip(chat = ""); 
	 	}
	 	else {
	 		mainForm.show(); 
	 		win.setForeground(mainForm.hwnd);
	 	} 
		
	};
}

import config;
import app.util;
mainForm.importImTipStyle = function(path){ 
	var buf = string.loadBuffer(path);
	if(!#buf) return mainForm.msgboxErr("此文件不包含有效的 ImTip 外观配置方案!");
	
	var data = string.match(buf,"\/\*ImTipConfig\{\{\*\/(.+)\/\*\}\}\*\/");
	if(!#data) return mainForm.msgboxErr("此文件不包含有效的 ImTip 外观配置方案!");
	
	try{
		var cfg = eval(data);  
		app.util.updateTipStyle(cfg);
		imeBar.imeSkin(cfg);	
		
 		config.style.embeddingFontData = null;
		config.style.embeddingFontName = null; 
		config.style.openStyle = null; 
		config.style.border = null;
		config.style.embeddingFontData = null;
		config.style.embeddingFontName = null; 
		config.style.openStyle = null; 
		config.style.border = null;
		config.style.background = null;
		config.style.foreground = null;
		config.style.linearGradient = null;
		config.style.paddingLeft = null;
		config.style.paddingRight = null;
		config.style.paddingTop = null;
		config.style.paddingBottom = null;
		
 		table.assign(config.style,cfg); 
 		config.style.save();	
	}
	catch(e){
		mainForm.msgboxErr(e);
	} 
}

mainForm.onCopyData = function(data,dataType){
 
	if(data=="start"){
		if(mainSettingForm){
			return win.setForeground(mainSettingForm.hwnd); 
		}
		
		var frmChild = mainForm.loadForm("\dlg\frmSetting.aardio");
		frmChild.show(); 
	}
	elseif(string.endWith(data,".aardio",true) && io.exist(data) ){
		if(mainSettingForm) { 
			win.setForeground(mainSettingForm.hwnd);
			return mainSettingForm.importStyleFromFile(data);
		}
		
		mainForm.importImTipStyle(data);  
	}
	elseif(data=="ime-enable"){
		imeBar.imeWatch(true);
	}
	elseif(data=="ime-disable"){
		imeBar.imeWatch(false);
	}
	elseif(data=="ime-switch"){
		imeBar.imeWatch(!!..imeBar.paused);
	}
	else {
		mainForm.show();
	}
}

mainForm.onClose = function(){
    mainForm.show(false);
    return true; 
} 

if(config.style.width){
	app.util.updateTipStyle(config.style);
	imeBar.imeSkin(config.style)
}

if(__argvImTipConfig){
	win.sendCopyData(mainForm.hwnd,__argvImTipConfig);
}

if(config.hotkey.chkEnableHotkey){
	import app.hotkey;
	app.hotkey.update(true);
}

mainForm.btnImTip.oncommand = function(id,event){
 
	if(..mainHotkeyForm) ..mainHotkeyForm.hitMin(); 
	if(!..mainSettingForm) ..mainSettingForm = ..mainForm.loadForm("\dlg\frmSetting.aardio"); 
	..mainSettingForm.show();  
	mainForm.show(false);
}

mainForm.btnAiChat.oncommand = function(id,event){
	
 	import process.imTip; 
	process.imTip(chat = ""); 
	
	mainForm.show(false);
}

mainForm.btnHotkey.oncommand = function(id,event){
	mainForm.btnHotkey.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250';text=''}
	import app.hotkey;
	app.hotkey.edit();
	
	thread.delay(1500);
	mainForm.btnHotkey.disabledText = null;
	mainForm.show(false);
}

mainForm.chkSysRun.checked = config.setting.sysRun;
mainForm.chkSysRun.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		error("此功能发布后可用。")
	}
	
	import sys.run;
	import process.admin;
	
	if( process.admin.isRunAs() &&  ! _WINXP ){
		import sys.runAsTask;
		
		sys.run("ImTip").delete();
		
		if(owner.checked){
			sys.runAsTask("ImTip","桌面助手").register("/sys");
			config.setting.sysRunAs = true;
		}
		else {
			sys.runAsTask("ImTip","桌面助手").delete();
			config.setting.sysRunAs = null;
		} 	
		
		mainForm.chkSysRun.text = "允许开机以管理权限启动"
	}
	else{
		if( config.setting.sysRunAs ){
			mainForm.msgboxErr('之前创建的开机启动项拥有管理权限，\n以相同的管理权限启动 ImTip 才能移除。')
			owner.checked = true;
			return ; 
		}
		
		if(owner.checked){
			sys.run("ImTip").register("/sys");
		}
		else {
			sys.run("ImTip").delete()
		} 
		
		mainForm.chkSysRun.text = "允许开机以普通权限启动（当前进程无管理权限）"
	}
	
	config.setting.sysRun = owner.checked;
	config.setting.save();
}

mainForm.chkHotkey.checked = ..config.hotkey.chkEnableHotkey;
mainForm.chkHotkey.oncommand = function(id,event){
	..config.hotkey.chkEnableHotkey = !..config.hotkey.chkEnableHotkey;
	
	import app.hotkey;
	app.hotkey.update(..config.hotkey.chkEnableHotkey);	
} 

var thrdWatcherFilterWindows;
mainForm.btnFilterWindows.skin(style.transButton)
mainForm.btnFilterWindows.oncommand = function(id,event){
	mainForm.btnFilterWindows.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250';text=''}
	
	import app.filterWindows;
	app.filterWindows.edit(); 
	
	thread.delay(1500);
	mainForm.btnFilterWindows.disabledText = null;
}

mainForm.chkFilterWindows.oncommand = function(id,event){
	config.setting.filterWindows = owner.checked;
	config.setting.save();
	
	mainForm.btnFilterWindows.disabled = !config.setting.filterWindows;
	import app.filterWindows;
	app.filterWindows.update();
}
mainForm.chkFilterWindows.checked = config.setting.filterWindows;
if(config.setting.filterWindows){
	import app.filterWindows;
	app.filterWindows.update();
	
	mainForm.btnFilterWindows.disabled = false;
}

mainForm.chkWatchFullScreen.checked = config.setting.watchFullScreen;
mainForm.chkWatchFullScreen.oncommand = function(id,event){
	config.setting.watchFullScreen = owner.checked;
	config.setting.save();
	
	import app.filterWindows;
	app.filterWindows.watchFullScreen(config.setting.watchFullScreen);	
}
if(config.setting.watchFullScreen){
	import app.filterWindows;
	app.filterWindows.watchFullScreen(true);	
}

mainForm.chkImeBarDisabled.oncommand = function( id,event ){
	..config.setting.imeBarDisabled =  !mainForm.chkImeBarDisabled.checked; 
	..imeBar.imeWatch(!..config.setting.imeBarDisabled);
	..config.setting.save();
}

mainForm.chkJab.oncommand = function(id,event){
	if(owner.checked){
		import app.jab;
		owner.checked = app.jab.enable(true);
		
		if(owner.checked){
 			config.setting.jab = true; 
 			mainForm.chkJab.text = "已启用 java.accessBridge 扩展，如未生效请重启目标程序或操作系统"
		}
		else {
			mainForm.chkJab.text = "未启用 java.accessBridge 扩展"
		}
		
	}
	else {
		config.setting.jab = null; 
		mainForm.msgbox("禁用 java.accessBridge 扩展在重启 ImTip 后生效！")
	}
	
	config.setting.save();
	
}

if(config.setting.jab){
	mainForm.chkJab.checked = true;
	import app.jab;
	app.jab.enable(true);
} 

 
if(..config.setting.imeBarDisabled){
	imeBar.imeWatch(false);  
}
else {
	mainForm.chkImeBarDisabled.checked = true; 
}

//现在是无边框窗体，在右上角用 plus 控件加一个关闭按钮，skin 配置好样式


mainForm.btnClose.skin({
    background = {
        hover = 0xFFE81123;
        active = 0xFFF1707A;
        default = 0x00FFFFFF;
    };
    color = {
        hover = 0xFFFFFFFF;
        default = 0xFFFFFFFF;
    }
})

mainForm.btnClose.oncommand = function(id,event){
    mainForm.close();
}

mainForm.onMouseDown  = function(wParam,lParam){ 
    mainForm.hitCaption();
}

win.loopMessage();